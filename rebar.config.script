%% -*- mode: erlang -*-

%% based on https://github.com/basho/webmachine/commit/a907e90086b39dcddf602e09a9ea1dc57ba675e4

%% Merge the list values in `ToAdd' into the list found at key `Key'
%% in proplist `C'. Don't duplicate items. New Items are added to the
%% front of existing items. It is an error if the value at `Key' is
%% not a list in `C'.
MergeConfig = fun({Key, ToAdd}, C) ->
                  case lists:keyfind(Key, 1, C) of
                    false ->
                      lists:keystore(Key, 1, C, {Key, ToAdd});
                    {Key, List} when is_list(List) ->
                      %% remove items in ToAdd already in List
                      ToAdd1 = [ I || I <- ToAdd, not lists:member(I, List) ],
                      lists:keystore(Key, 1, C, {Key, ToAdd1 ++ List })
                  end
              end.

%% -- Add development only options if `KATT_MODE' =:= "DEV" --
%% * add the deps from dev_only_deps defined in rebar.config
%% * define the compiler macro DEV_ONLY

DevOnlyConfig = (
  fun(true) ->
      DevOnlyDeps = case lists:keyfind(dev_only_deps, 1, CONFIG) of
                      false ->
                        [];
                      {dev_only_deps, DOD} ->
                        DOD
                    end,
      [ {deps, DevOnlyDeps}
      , {erl_opts, [ {d, 'DEV_ONLY'}
                   , debug_info
                   ]}
      ];
     (false) ->
      []
  end
 )(os:getenv("KATT_MODE") =:= "DEV").

%% -- Add optional options unless `KATT_MODE' =:= "BARE" --
%% * add the deps from optional_deps defined in rebar.config

OptionalConfig = (
  fun(false) ->
      OptionalDeps = case lists:keyfind(optional_deps, 1, CONFIG) of
                       false ->
                         [];
                       {optional_deps, DOD} ->
                         DOD
                     end,
      [{deps, OptionalDeps}];
     (true) ->
      [{erl_opts, [ {d, 'BARE_MODE'}
                  , debug_info
                  ]}
      ]
  end
 )(os:getenv("KATT_MODE") =:= "BARE").

lists:foldl(MergeConfig, CONFIG, DevOnlyConfig ++ OptionalConfig).
